#!/usr/bin/env perl
# 2014-04-02, Adam Bryt
# Filtr do zamiany tekstu ascii utf8 na pdf.

use strict;
use warnings;
use v5.14;

use File::Temp qw(tempdir);
use Try::Tiny;
use File::Basename qw(basename);

our $PDFLATEX = "pdflatex";
our $BASENAME = "document";
our $PROGNAME = basename($0);
our $Opts = {};
our $Conf = {};

our $USAGE = <<EOS;
usage: $PROGNAME [-o FILE] [FILE ...]
EOS

sub init_signals {
    $SIG{__WARN__} = sub {
        my ($m) = @_;
        warn "$PROGNAME: $m";
    };
}

sub usage {
    print STDERR $USAGE;
    exit 1;
}

sub fatal {
    my ($m) = @_;

    $m = "$PROGNAME: $m";
    $m .= "\n" if substr($m, -1) ne "\n";
    print STDERR $m;
    exit 1;
}

sub parse_options {
    use Getopt::Long;
    Getopt::Long::Configure("bundling");

    GetOptions($Opts, "o=s")
        or usage();
}

sub parse_config {
    use File::Slurp;
    my $str;
    try {
        $str = read_file(\*DATA);
    } catch {
        fatal "can't read config data from __DATA__: $_";
    };

    use YAML;
    try {
        our $Conf = Load($str);
    } catch {
        fatal "can't parse YAML config data: $_";
    };
}

sub create_tex_document {
    my ($dir) = @_;

    my $fname = $dir . "/" . $BASENAME . ".tex";

    open(my $file, ">", $fname)
        or fatal "can't open file '$fname': $!";

    print $file $Conf->{docprefix};
    print $file $_ while <>;
    print $file $Conf->{docsuffix};

    close $file;
}

sub compile_tex_document {
    my ($dir) = @_;

    use Cwd qw(getcwd);
    my $olddir = getcwd();

    chdir($dir)
        or fatal "can't chdir to $dir: $!";

    my $docname = "$BASENAME.tex";
    my $logname = "a2pdf.log";
    my $cmd = "$PDFLATEX $docname > $logname";

    system($cmd) == 0
        or fatal "error in command: $cmd: $!: $?";

    chdir($olddir)
        or fatal "can't chdir to $olddir $!";
}

sub dump_pdf {
    my ($dir) = @_;

    my $pdfname = "$dir/$BASENAME.pdf";
    open(my $pdffh, "<", $pdfname)
        or fatal "can't open file for reading: $pdfname $!";

    my $outfh;
    my $outname = $Opts->{o};
    if (defined $outname and $outname ne "-") {
        open($outfh, ">", $outname)
            or fatal "error in writing file: $outname: $!";
    }
    else {
        $outfh = \*STDOUT;
    }

    my $bufsize = 4096;
    my $buf;
    while (1) {
        my $stat = read($pdffh, $buf, $bufsize);

        if (! defined($stat)) {
            fatal "error in reading file: $pdfname: $!";
        }
        if ($stat == 0) { last }

        print $outfh $buf;
    }
    
    close $outfh if $outfh != \*STDOUT;
    close $pdffh;
}

sub main {
    init_signals();
    parse_options();
    parse_config();

    $File::Temp::KEEP_ALL = 1;

    my $dir = tempdir(TMPDIR => 1, TEMPLATE => "a2pdf_XXXXXX");
    say STDERR "dir: $dir";

    create_tex_document($dir);
    compile_tex_document($dir);
    dump_pdf($dir);
}

main();


=encoding utf8

=head1 NAME

B<a2pdf> - konwertuje tekst do PDF

=head1 SYNOPSIS

B<a2pdf> [B<-o> I<FILE>] [I<FILE> I<...>]

=head1 DESCRIPTION

Skrypt B<a2pdf> jest filtrem konwertującym tekst (plain text) do formatu
PDF. Potrafi konwertować tekst z polskimi znakami kodowanymi w UTF-8.
Zastosowaniem filtru może być drukowanie z edytora Vim.

Do konwersji jest używany C<pdflatex> -- tworzony jest tymczasowy
dokument LaTeXa z konwertowanym tekstem umieszczonym w środowisku
C<verbatim>, dokument jest kompilowany przez C<pdflatex> i zwracany jest
wynikowy PDF.

=head1 OPTIONS

=over

=item B<-o> I<FILE>

Opcja powoduje zapisanie wynikowego PDFu do pliku I<FILE> (output file).
Jeśli nazwą pliku I<FILE> jest "-", lub nie ma opcji B<-o>, to wynikowy
PDF jest wysyłany na I<STDOUT>.

=back

=head1 EXAMPLES

Poniższe trzy przykłady są równoważne i powodują konwersję pliku
tekstowego C<x.txt> do formatu PDF i zapisanie go w pliku C<x.pdf>:

    $ a2pdf -o x.pdf x.txt
    $ cat x.txt | a2pdf > x.pdf
    $ cat x.txt | a2pdf -o x.pdf

=head1 AUTHOR

Adam Bryt <adam.bryt at gmx dot com>, 2014-04-05

=cut


__DATA__

---
docprefix: |
    \documentclass[a4paper,11pt]{article}

    \usepackage{polski}
    \usepackage[utf8]{inputenc}
    \usepackage[T1]{fontenc}
    \usepackage{lmodern}

    % Ustawienie marginesów:
    \usepackage[margin=1in]{geometry}

    % Konfiguracja nagłówków i stopek:
    \usepackage{fancyhdr}
    %\lhead{\slshape \leftmark}
    %\chead{}
    %\rhead{}
    %\lfoot{}
    %\cfoot{\thepage}
    %\rfoot{}
    %\renewcommand{\headrulewidth}{0.4pt}
    %\renewcommand{\footrulewidth}{0pt}
    \pagestyle{fancy}
    \setlength{\headheight}{14pt}  % Poprawia warningi.

    % Aktywne linki w dokumencie PDF:
    \usepackage{bookmark}
    \usepackage{hyperref}
    \hypersetup{
      pdftitle={},
      pdfsubject={},
      pdfauthor={},
      hidelinks,
      pdfdisplaydoctitle=true,
      colorlinks,
      allcolors=blue,
    }

    \begin{document}
    \begin{verbatim}
docsuffix: |
    \end{verbatim}
    \end{document}
